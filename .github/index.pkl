//===----------------------------------------------------------------------===//
// Copyright Â© 2025 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//   https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//

amends "@pkl.impl.ghactions/PklCI.pkl"

import "@com.github.actions/catalog.pkl"
import "@com.github.actions/context.pkl"
import "@com.github.actions/Workflow.pkl"
import "@pkl.impl.ghactions/helpers.pkl"

local buildArtifactsName = "pkl-lsp-build-artifacts"

testReports {
  junit {
    "build/test-results/**/*.xml"
  }
  excludeJobs {
    Regex("deploy-.*")
  }
}

local buildAndTest: Workflow = new {
  jobs {
    ["build"] = buildJob(false)
  }
}

local function buildJob(isRelease: Boolean): Workflow.Job = new {
  `runs-on` = "ubuntu-latest"
  steps {
    (catalog.`actions/checkout@v6`) {
      with {
        `fetch-depth` = 0 // needed for spotless
      }
    }
    (catalog.`actions/setup-java@v5`) {
      with {
        `java-version` = "21"
        distribution = "temurin"
        cache = if (isRelease) null else "gradle"
      }
    }
    new {
      `if` = "github.repository != 'apple/pkl-lsp'"
      run =
        """
        git remote add upstream https://github.com/apple/pkl-lsp.git
        git fetch upstream main
        echo 'PKL_ORIGINAL_REMOTE_NAME=upstream' > "$GITHUB_ENV"
        """
    }
    new {
      local args: Listing<String> = new {
        "--info"
        "--stacktrace"
        when (isRelease) {
          "-DreleaseBuild=true"
        }
      }
      run =
        """
        ./gradlew \(args.join(" ")) check shadowJar verifyDistribution
        """
    }
    (catalog.`actions/upload-artifact@v5`) {
      name = "Upload build artifacts"
      `if` = "!cancelled()"
      with {
        name = buildArtifactsName
        path = "build/libs/"
      }
    }
  }
}

local function verifyJobs(requiresJob: String): Mapping<String, Workflow.Job> = new {
  default {
    needs { requiresJob }
    steps {
      (catalog.`actions/checkout@v6`) {}
      (catalog.`actions/setup-java@v5`) {
        with {
          `java-version` = "21"
          distribution = "temurin"
          cache = if (requiresJob.contains("release")) null else "gradle"
        }
      }
      (catalog.`actions/download-artifact@v6`) {
        with {
          name = buildArtifactsName
          path = "build/libs/"
        }
      }
      new {
        run =
          """
          ./gradlew --info --stacktrace verifyDistribution
          """
      }
    }
  }
  ["test-macos"] {
    `runs-on` = new Listing { "self-hosted"; "macos" }
    `if` = "github.repository_owner == 'apple'"
  }
  ["test-windows"] {
    `runs-on` = "windows-latest"
  }
}

local verifyJobNames = verifyJobs("build").keys.toListing()

local baseMavenDeploy: Workflow.Job = new {
  `runs-on` = "ubuntu-latest"
  environment = "maven-release"
  steps {
    (catalog.`actions/checkout@v6`) {}
    (catalog.`actions/setup-java@v5`) {
      with {
        `java-version` = "21"
        distribution = "temurin"
        cache = null
      }
    }
    (catalog.`actions/download-artifact@v6`) {
      with {
        name = buildArtifactsName
        path = "build/libs/"
      }
    }
  }
}

prb {
  jobs {
    ["build"] = buildJob(false)
  }
}

build {
  jobs {
    ["build"] = buildJob(false)
    ...(verifyJobs("build")) {
    // TODO use lower priority runners for on-push macOS jobs
    // ["test-macos"] { `runs-on` { "nightly" } }
    }
  }
}

releaseBranch = build

main = (build) {
  jobs {
    ["deploy-maven-snapshot"] = (baseMavenDeploy) {
      needs = verifyJobNames
      steps {
        new Workflow.Step {
          name = "Publish snapshot release"
          run = "./gradlew publishToSonatype"
        } |> helpers.withMavenPublishSecrets
      }
    }
  }
}

release {
  jobs {
    ["build-release"] = buildJob(true)
    ...verifyJobs("build-release")
    // do GitHub release first because we can overwrite the tag.
    // publishing to Maven Central is final.
    ["deploy-github-release"] {
      `runs-on` = "ubuntu-latest"
      needs = verifyJobs("build-release").keys.toListing()
      permissions {
        contents = "write"
      }
      steps {
        (catalog.`actions/download-artifact@v6`) {
          with {
            name = buildArtifactsName
            path = "build/libs/"
          }
        }
        new {
          name = "Publish release to GitHub"
          env {
            ["GH_TOKEN"] = context.github.token
            ["GH_REPO"] = context.github.repository
          }
          run =
            #"""
            gh release create "\#(context.github.refName)" \
              --title "\#(context.github.refName)" \
              --target "\#(context.github.sha)" \
              --verify-tag \
              --notes "Release notes: https://pkl-lang.org/lsp/current/CHANGELOG.html#release-\#(context.github.refName)" \
              build/libs/*
            """#
        }
      }
    }
    ["deploy-maven-release"] = (baseMavenDeploy) {
      needs { "deploy-github-release" }
      steps {
        new Workflow.Step {
          name = "Publish to Maven Central"
          run =
            // language=bash
            """
            ./gradlew -DreleaseBuild=true publishToSonatype closeAndReleaseSonatypeStagingRepository
            """
        } |> helpers.withMavenPublishSecrets
      }
    }
  }
}
