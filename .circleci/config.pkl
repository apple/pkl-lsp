amends "package://pkg.pkl-lang.org/pkl-project-commons/pkl.impl.circleci@1.1.1#/PklCI.pkl"

prb {
  jobs {
    "build"
    ...verifyJobs
  }
}

local verifyJobs: Listing<Mapping<String, WorkflowJob>> = new {
  for (os in List("linux", "macos", "windows")) {
    new {
      ["test-\(os)"] {
        requires {
          "build"
        }
      }
    }
  }
}

main {
  jobs {
    "build"
    ...verifyJobs
  }
}

circleCi {
  workflows {
    ["ci-setup"] {
      jobs {
        "build"
        ...verifyJobs
      }
      `when` {
        equal {
          "<< pipeline.git.branch >>"
          "ci-setup"
        }
      }
    }
  }
}

local verifyJob: Job = new {
  steps {
    "checkout"
    new AttachWorkspaceStep { at = "." }
    new RunStep {
      command = """
        ./gradlew --info --stacktrace -DtestReportsDir="${HOME}/test-results" verifyDistribution
        """
    }
    new StoreTestResults { path = "~/test-results" }
  }
}

local function buildJob(isRelease: Boolean): Job = new {
  docker {
    new {
      image = "cimg/openjdk:21.0"
    }
  }
  steps {
    "checkout"
    new RunStep {
      local args: Listing<String> = new {
        "--info"
        "--stacktrace"
        #"-DtestReportsDir="${HOME}/test-results""#
        when (isRelease) {
          "-DreleaseBuild=true"
        }
      }
      command = """
        ./gradlew \(args.join(" ")) check shadowJar
        """
    }
    new PersistToWorkspaceStep {
      root = "."
      paths {
        "build/libs/"
      }
    }
    new StoreTestResults { path = "~/test-results" }
  }
}

jobs {
  ["build"] = buildJob(false)
  ["build-release"] = buildJob(true)
  ["test-linux"] = (verifyJob) {
    docker {
      new {
        image = "cimg/openjdk:21.0"
      }
    }
  }
  ["test-macos"] = (verifyJob) {
    macos {
      xcode = "15.3.0"
    }
    resource_class = "macos.m1.large.gen1"
  }
  ["test-windows"] = (verifyJob) {
    machine {
      image = "windows-server-2022-gui:current"
    }
    resource_class = "windows.large"
    shell = "bash.exe"
  }
  ["do-release"] {
    docker {
      new { image = "maniator/gh:v2.40.1" }
    }
    steps {
      new AttachWorkspaceStep {
        at = "."
      }
      new RunStep {
        name = "gh release"
        command = """
          echo "release"
          """
      }
    }
  }
}

local buildWorkflow = new Workflow {
  jobs {
    "build"
  }
}